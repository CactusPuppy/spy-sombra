import "OSTWUtils/String.del";
import "OSTWUtils/OnScreenText.del";

globalvar Number i;
globalvar Button PREVIOUS_DISGUISE_BUTTON = Button.PrimaryFire;
globalvar Button NEXT_DISGUISE_BUTTON = [
  Button.Ability1,
  Button.SecondaryFire
][WorkshopSettingCombo("1. Settings", "Next Disguise Button", 0, [
  "Ability 1 - Virus",
  "Secondary Fire - Hack"
], 0)];

enum DisguiseMenuState {
  CLOSED = "Closed",
  OPEN = "Open",
  DISGUISED = "Disguised"
}
enum DisguiseMenuEvent {
  NONE = "None",
  DROPPED_DISGUISE = "Dropped Disguise",
  REQUEST_OPEN = "Request Open",
  REQUEST_CLOSE = "Request Close",
  SELECT_DISGUISE = "Select Disguise"
}
playervar DisguiseMenuState disguiseMenuState = DisguiseMenuState.CLOSED;
playervar DisguiseMenuEvent disguiseMenuEvent = DisguiseMenuEvent.NONE;

playervar Number disguiseSelectionIndex; // The player's selection within the disguise menu
playervar Player disguiseTarget;
playervar Any[] disguiseMenuTexts = [];
playervar Player[] disguisableOpps;

Number MENU_SPACING: 0.7;
Number MENU_MIDPOINT(in Player p = EventPlayer()): p.disguisableOpps.Length / 2;
Number MENU_ITEM_X(in Player p = EventPlayer(), in Number i): (i - MENU_MIDPOINT(p)) / 2;
Number MENU_Y: 0.5;
Team opps(Player p = EventPlayer()): OppositeTeamOf(TeamOf(p));
Player[] disguisableOppsRaw(Player p = EventPlayer()): AllPlayers(opps(p)).Filter(p2 => SlotOf(p2) < 5 && HasSpawned(p2));

rule: "Disable Inspector if not debugging" -100
if (DEBUG_MODE == false)
{
  DisableInspectorRecording();
}

rule: 'Disable scoreboard (it gives the bot away)'
Event.OngoingPlayer
if (DEBUG_MODE == false)
if (IsGameInProgress())
{
  DisableScoreboard();
  WaitUntil(!IsGameInProgress(), 1000000);
  EnableScoreboard();
}

rule: "When a Sombra holds Interact, open the disguise menu"
Event.OngoingPlayer
Player.Sombra
if (IsButtonHeld(EventPlayer(), Button.Interact))
{
  Wait(0.25, WaitBehavior.AbortWhenFalse);
  AbortIf(!IsAlive());
  if (!(IsGameInProgress() || DEBUG_MODE)) {
    SmallMessage(EventPlayer(), <" <0> Cannot disguise until round begins", IconString(Icon.Warning)>);
    return;
  }
  TriggerDMEvent(DisguiseMenuEvent.REQUEST_OPEN);
  WaitUntil(!IsButtonHeld(EventPlayer(), Button.Interact), 1000000);
  if (disguiseSelectionIndex < disguisableOpps.Length) {
    TriggerDMEvent(DisguiseMenuEvent.SELECT_DISGUISE);
  } else {
    TriggerDMEvent(DisguiseMenuEvent.DROPPED_DISGUISE);
  }
}

rule: "When Sombra translocates, cancel Stealth if set up"
Event.OngoingPlayer
Player.Sombra
if (WorkshopSettingToggle("1. Settings", "Remove Stealth from Translocator", true, 2))
if (IsUsingAbility2())
{
  WaitUntil(HasStatus(EventPlayer(), Status.PhasedOut), 0.35);
  AbortIf(!HasStatus(EventPlayer(), Status.PhasedOut));
  WaitUntil(!HasStatus(EventPlayer(), Status.PhasedOut), 0.5);
  Wait(0.5);
  PressButton(EventPlayer(), Button.Melee);
}

rule: "When a Sombra swaps off Sombra, drop their disguise"
Event.OngoingPlayer
if (HeroOf() == Hero.Sombra)
{
  WaitUntil(HeroOf() != Hero.Sombra, 1000000);
  TriggerDMEvent(DisguiseMenuEvent.DROPPED_DISGUISE);
}

rule: "Break disguise when Sombra does aggressive action"
Event.OngoingPlayer
Player.Sombra
if (IsFiringPrimary(EventPlayer())
  || IsMeleeing(EventPlayer())
  || IsUsingAbility1(EventPlayer())
  || IsUsingAbility2(EventPlayer())
  || IsUsingUltimate(EventPlayer())
)
{
  TriggerDMEvent(DisguiseMenuEvent.DROPPED_DISGUISE);
}

rule: "Break disguise when the Sombra dies"
Event.OnDeath
if (!IsDummyBot())
{
  TriggerDMEvent(DisguiseMenuEvent.DROPPED_DISGUISE);
}

rule: "Break disguise when between rounds or match complete"
Event.OngoingPlayer
Player.Sombra
if (IsBetweenRounds() || IsMatchComplete())
{
  TriggerDMEvent(DisguiseMenuEvent.DROPPED_DISGUISE);
}

rule: "Move disguise selection to the previous option"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), PREVIOUS_DISGUISE_BUTTON))
{
  AbortIf(disguiseMenuState != DisguiseMenuState.OPEN);
  disguiseSelectionIndex -= 1;
  if (disguiseSelectionIndex < 0) {
    disguiseSelectionIndex = disguisableOpps.Length;
  }
}

rule: "Move disguise selection to the next option"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), NEXT_DISGUISE_BUTTON))
{
  AbortIf(disguiseMenuState != DisguiseMenuState.OPEN);
  disguiseSelectionIndex += 1;
  if (disguiseSelectionIndex > disguisableOpps.Length) {
    disguiseSelectionIndex = 0;
  }
}

void OpenDisguiseMenu() playervar "[SUB]: Open the disguise menu for the player" {
  if (PREVIOUS_DISGUISE_BUTTON == Button.PrimaryFire) SetPrimaryFireEnabled(EventPlayer(), false);
  DisallowButton(EventPlayer(), PREVIOUS_DISGUISE_BUTTON);
  DisallowButton(EventPlayer(), NEXT_DISGUISE_BUTTON);
  disguisableOpps = disguisableOppsRaw();
  if (EntityExists(disguiseTarget))
    disguiseSelectionIndex = disguisableOpps.IndexOf(disguiseTarget);
  OnScreenText.CreateOnScreenText(
    VisibleTo:            EventPlayer(),
    Header:               <"< <0>", InputBindingString(PREVIOUS_DISGUISE_BUTTON)>,
    PositionX:            MENU_ITEM_X(EventPlayer(), -1),
    PositionY:            MENU_Y + 0.15,
    Scale:                3,
    Reevaluation:         InworldTextRev.VisibleToPositionStringAndColor,
    Color:                IsButtonHeld(EventPlayer(), PREVIOUS_DISGUISE_BUTTON) ? Color.Green : Color.Gray,
  );
  disguiseMenuTexts.ModAppend(LastTextID());
  OnScreenText.CreateOnScreenText(
    VisibleTo:            EventPlayer(),
    Header:               <"<0> >", InputBindingString(NEXT_DISGUISE_BUTTON)>,
    PositionX:            MENU_ITEM_X(EventPlayer(), disguisableOpps.Length + 1),
    PositionY:            MENU_Y + 0.15,
    Scale:                3,
    Reevaluation:         InworldTextRev.VisibleToPositionStringAndColor,
    Color:                IsButtonHeld(EventPlayer(), NEXT_DISGUISE_BUTTON) ? Color.Yellow : Color.Gray,
  );
  disguiseMenuTexts.ModAppend(LastTextID());
  for (i = 0; disguisableOpps.Length; 1) {
    OnScreenText.CreateOnScreenText(
      VisibleTo:          EntityExists(disguisableOpps[EvaluateOnce(i)]) ? EventPlayer() : null,
      Header:             $" {HeroIconString(HeroOf(disguisableOpps[EvaluateOnce(i)]))} ",
      PositionX:          MENU_ITEM_X(EventPlayer(), EvaluateOnce(i)),
      PositionY:          MENU_Y + 0.25,
      Scale:              4,
      Reevaluation:       InworldTextRev.VisibleToPositionAndString,
      Color:              Color.White,
      Spectators:         Spectators.DefaultVisibility,
      PerspectivePlayer:  EventPlayer(),
    );
    disguiseMenuTexts.ModAppend(LastTextID());
    OnScreenText.CreateOnScreenText(
      VisibleTo:          EntityExists(disguisableOpps[EvaluateOnce(i)]) ? EventPlayer() : null,
      Header:             <"<1><0><2>",
                            disguisableOpps[EvaluateOnce(i)],
                            disguiseSelectionIndex == EvaluateOnce(i) ? "[" : "",
                            disguiseSelectionIndex == EvaluateOnce(i) ? "]" : "">,
      PositionX:          MENU_ITEM_X(EventPlayer(), EvaluateOnce(i)),
      PositionY:          MENU_Y,
      Scale:              1.25,
      Reevaluation:       InworldTextRev.VisibleToPositionStringAndColor,
      Color:              disguiseSelectionIndex == EvaluateOnce(i) ? Color.Green : Color.White,
      Spectators:         Spectators.DefaultVisibility,
      PerspectivePlayer:  EventPlayer(),
    );
    disguiseMenuTexts.ModAppend(LastTextID());
  }
  OnScreenText.CreateOnScreenText(
    VisibleTo:            EventPlayer(),
    Header:               $" {IconString(Icon.No)} ",
    PositionX:            MENU_ITEM_X(EventPlayer(), disguisableOpps.Length),
    PositionY:            MENU_Y + 0.25,
    Scale:                4,
    Reevaluation:         InworldTextRev.VisibleToAndPosition,
    Color:                Color.White,
    Spectators:           Spectators.DefaultVisibility,
    PerspectivePlayer:    EventPlayer(),
  );
  disguiseMenuTexts.ModAppend(LastTextID());
  OnScreenText.CreateOnScreenText(
    VisibleTo:            EventPlayer(),
    Header:               <"<0>No Disguise<1>",
                            disguiseSelectionIndex == disguisableOpps.Length ? "[" : "",
                            disguiseSelectionIndex == disguisableOpps.Length ? "]" : "">,
    PositionX:            MENU_ITEM_X(EventPlayer(), disguisableOpps.Length),
    PositionY:            MENU_Y,
    Scale:                1.25,
    Reevaluation:         InworldTextRev.VisibleToPositionStringAndColor,
    Color:                disguiseSelectionIndex == disguisableOpps.Length ? Color.Red : Color.White,
    Spectators:           Spectators.DefaultVisibility,
    PerspectivePlayer:    EventPlayer(),
  );
  disguiseMenuTexts.ModAppend(LastTextID());
}

void CloseDisguiseMenu() playervar "[SUB]: Close the disguise menu for the player" {
  if (PREVIOUS_DISGUISE_BUTTON == Button.PrimaryFire) SetPrimaryFireEnabled(EventPlayer(), true);
  AllowButton(EventPlayer(), PREVIOUS_DISGUISE_BUTTON);
  AllowButton(EventPlayer(), NEXT_DISGUISE_BUTTON);
  while (disguiseMenuTexts.Length > 0) {
    DestroyInWorldText(disguiseMenuTexts.First);
    disguiseMenuTexts.ModRemoveByIndex(0);
  }
}

import "disguise.del";
import "stateMachine.del";

import "debug.del";
